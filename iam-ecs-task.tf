resource "aws_iam_role" "ecs_task" {
  count              = var.create_iam_role ? 1 : 0
  name               = "ecs-task-openvpn-${var.name}"
  assume_role_policy = data.aws_iam_policy_document.ecs_task_assume_role.json
  tags               = var.tags
}

resource "aws_iam_role_policy_attachment" "ecs_task_execution" {
  count      = var.create_iam_role ? 1 : 0
  role       = aws_iam_role.ecs_task[0].name
  policy_arn = "arn:aws:iam::aws:policy/service-role/AmazonECSTaskExecutionRolePolicy"
}

resource "aws_iam_role_policy" "ssm_policy" {
  count = var.create_iam_role ? 1 : 0
  name = "ecs-ssm-policy"
  role = aws_iam_role.ecs_task[0].name

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Effect   = "Allow"
        Action   = ["ssm:GetParameters", "ssm:GetParameter", "ssm:GetParametersByPath"]
        Resource = ["arn:aws:ssm:*:*:parameter/*"]
      }
    ]
  })
}

resource "aws_iam_role_policy" "s3_policy" {
  count = var.create_iam_role ? 1 : 0
  name = "ecs-s3-policy"
  role = aws_iam_role.ecs_task[0].name

  policy = jsonencode({
    Version = "2012-10-17"
    Statement = [
      {
        Sid      = "ListBucket"
        Effect   = "Allow"
        Action   = ["s3:ListBucket", "s3:GetBucketLocation"]
        Resource = [aws_s3_bucket.vpn.arn]
      },
      {
        Sid    = "ObjectRW"
        Effect = "Allow"
        Action = [
          "s3:GetObject",
          "s3:PutObject",
          "s3:DeleteObject",
          "s3:AbortMultipartUpload",
          "s3:ListMultipartUploadParts"
        ]
        Resource = ["${aws_s3_bucket.vpn.arn}/*"]
      }
    ]
  })
}
